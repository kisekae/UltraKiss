; Kisekae UltraKiss configuration file created on Thu Oct 09 17:30:33 MDT 2025
; FKiSS code compatibility level is 5
 
;[Option Section]
; constrainfkiss = false
 
;[Screen Section]
[0                                                          
(600,600)                                                   
 
;[Palette Section]
%BALL.KCF                                                   
 
;[Cel Section]
#17   BALLY.GIF               : 2                           ;%u 
#17   BALL.GIF                : 2                           ;
#16   BALLR.GIF               : 2                           ;
#15   BALLR.GIF               : 2                           ;
#14   BALLR.GIF               : 2                           ;
#13   BALLR.GIF               : 2                           ;
#12   BALLG.GIF               : 2                           ;
#11   BALLG.GIF               : 2                           ;
#10   BALLG.GIF               : 2                           ;
#9    BALLG.GIF               : 2                           ;
#8    BALL.GIF                : 2                           ;
#7    BALL.CEL         *0.1   : 1                           ;
#7    BALL.CEL         *0     : 1                           ;
#7    BALL.CEL         *0     : 1                           ;
#7    BALL.CEL         *0     : 1                           ;
#7    BALL.CEL         *0     : 1                           ;
#7    BALL.CEL         *0     : 1                           ;
#7    BALL.CEL         *0     : 1                           ;
#7    BALL.CEL         *0     : 1                           ;
#7    BALL.CEL         *0     : 1                           ;
#19   button2-22.cel          : 2                           ;
#6    button2.cel             : 0 1                         ;
#19   button1-23.cel          : 2                           ;
#6    button1.cel             : 0 1                         ;
#3    button5.button          : 0                           ;%size[100,50] %offset[0,77] %attributes[text="button2"] 
#3    button4.button          : 0                           ;%size[100,50] %offset[1,0] %attributes[text="button1"] 
 
;[Page Set Section]
; Page 0
$0 * * * 160,226 * * 313,228 * * * * * * * * * * * * *
; Page 1
$0 * * * * * * 463,0 286,291 * * * * * * * * * * * *
; Page 2
$0 * * * * * * * * 250,250 250,200 200,250 250,300 300,250 250,150 250,350 150,250
   350,250 250,250 * 463,0
 
 
;[FKiSS Section]
;@EventHandler
 
;@initialize()                                              ; 
;@ let(G,#7)                                                ; G is circle object number
;@ let(C,#8)                                                ; C is cross center object number
;@ let(Ball,#17)                                            ; C is cross center object number
;@ let(T,500)                                               ; T is animation period
 
;@press("button4.button")                                   ; 
;@ changeset(1)                                             ; switch to page set 1

;@press("button5.button")                                   ; 
;@ changeset(2)                                             ; switch to page set 2

; Start and stop animation on page 1

;@press("button1.cel")                                      ; 
;@ timer(Rotate,0)                                          ; page 1 stop any animation 
;@ gosub(Init1)                                             ; set up circle 				                                  		

;@press("button2.cel")                                      ; 
;@ let(P,0)                                                 ; page 1 initialize animation
;@ timer(Rotate,1,0)                                        ; start rotation

; Start and stop animation on page 2

;@press("button1-23.cel")                                   ; 
;@ timer(Cross,0)                                           ; page 2 stop movement
;@ goto(Init2)                                              ; 

;@press("button2-22.cel")                                   ; 
;@ timer(Cross,1)                                           ; page 2 start movement

; Page 1 - Create circle of balls

;@label(Init1)                                              ; 
;@ let(R,50)                                                ; R is 50 pixels radius 
;@ div(S,6.28,9)                                            ; S is 1/9 of full circle segment 
;@ for(N,0,8)                                               ; loop for 9 balls 
;@  concat(V,"!#",G,":",N)                                  ; identify Nth cel frame "!#G:n"
;@  mul(A,S,N)                                              ; angle A to position Nth ball (N*S)
;@  math(Cos,"cos",A) math(Sin,"sin",A)                     ; 
;@  mul(X,R,Cos) mul(Y,R,Sin)                               ; x = R*cos(a), y = R*sin(a)
;@  moveto(V,X,Y)                                           ; move Nth ball onto circle
;@ next(N)                                                  ; 
;@ moveto(G,225,225)                                        ; move circle to center of playfield

; Page 2 - Create overlay moving ball

;@label(Init2)                                              ; 
;@ letobjectx(X,C) letobjecty(Y,C)                          ; position of center ball
;@ moveto(Ball,X,Y)                                         ; set new ball in center
;@ altmap(Ball)                                             ; make ball visible
                                             
; Page 1 - Change next ball color
 
;@alarm(Rotate,@N)                                          ; 
;@ while(P,0)                                               ; search for non-default ball
;@  concat(V1,"!#",G,":",@N)                                ; identify Nth cel frame "!#G:n"
;@  add(@N,@N,1) mod(@N,@N,9)                               ; @N is next ball (0-8)
;@  letpal(P,V1)                                            ; palette 0 is page default
;@ endwhile(P)                                              ; 
;@ concat(V2,"!#",G,":",@N)                                 ; identify N+1 cel frame "!#G:n"
;@ setpal(V2,P) setpal(V1,0) let(P,0)                       ; change next ball palette
;@ timer(Rotate,T,@N)                                       ; repeat in T ms

; Page 2 - Move overlay ball in cross

;@alarm(Cross)                                              ; 
;@ for(I,0,1) move(Ball,50,0) paint() sleep(T) next(I)      ; 
;@ for(I,0,3) move(Ball,-50,0) paint() sleep(T) next(I)     ; 
;@ for(I,0,1) move(Ball,50,0) paint() sleep(T) next(I)      ; 
;@ for(I,0,1) move(Ball,0,-50) paint() sleep(T) next(I)     ; 
;@ for(I,0,3) move(Ball,0,50) paint() sleep(T) next(I)      ; 
;@ for(I,0,1) move(Ball,0,-50) paint() sleep(T) next(I)     ; 
;@ timer(Cross,1)                                           ; 
