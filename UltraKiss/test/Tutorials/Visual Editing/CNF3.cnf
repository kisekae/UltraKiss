; Kisekae UltraKiss configuration file created on Sun Oct 05 12:16:50 MDT 2025
; FKiSS code compatibility level is 5
 
;[Option Section]
; constrainfkiss = false
 
;[Screen Section]
[0                                                          
(600,600)                                                   
 
;[Palette Section]
%BALL.KCF                                                   
 
;[Cel Section]
#7   BALL.CEL         *0.1   : 1                            ;
#7   BALL.CEL         *0     : 1                            ;
#7   BALL.CEL         *0     : 1                            ;
#7   BALL.CEL         *0     : 1                            ;
#7   BALL.CEL         *0     : 1                            ;
#7   BALL.CEL         *0     : 1                            ;
#7   BALL.CEL         *0     : 1                            ;
#7   BALL.CEL         *0     : 1                            ;
#7   BALL.CEL         *0     : 1                            ;
#6   button2.cel             : 0 1                          ;
#6   button1.cel             : 0 1                          ;
#3   button5.button          : 0                            ;%size[100,50] %offset[0,77] %attributes[text="button2"] 
#3   button4.button          : 0                            ;%size[100,50] %offset[1,0] %attributes[text="button1"] 
 
;[Page Set Section]
; Page 0
$0 * * * 160,226 * * 313,228 *
; Page 1
$0 * * * * * * 463,0 0,0
 
 
;[FKiSS Section]
;@EventHandler
 
;@initialize()                                              ; 
;@ let(G,7)                                                 ; G is circle object number
 
;@press("button1.cel")                                      ; 
;@ timer(Rotate,0)                                          ; stop any animation 
;@ gosub(Init)                                              ; set up circle 				                                  		
;@ moveto(G,225,225)                                        ; move circle to center of playfield

;@label(Init)                                               ; 
;@ let(R,50)                                                ; R is 50 pixels radius 
;@ div(S,6.28,9)                                            ; S is 1/9 of full circle segment 
;@ for(N,0,8)                                               ; loop for 9 balls 
;@  concat(V,"!#",G,":",N)                                  ; identify Nth cel frame "!#G:n"
;@  mul(A,S,N)                                              ; angle A to position Nth ball (N*S)
;@  math(Cos,"cos",A) math(Sin,"sin",A)                     ; 
;@  mul(X,R,Cos) mul(Y,R,Sin)                               ; x = R*cos(a), y = R*sin(a)
;@  moveto(V,X,Y)                                           ; move Nth ball onto circle
;@ next(N)                                                  ; 
 
;@press(#3)                                                 ; 
;@ changeset(1)                                             ; switch to page set 1
 
;@press("button2.cel")                                      ; 
;@ let(P,0)                                                 ; initialize 
;@ timer(Rotate,1,0)                                        ; start rotation
 
; Change next ball color
 
;@alarm(Rotate,@N)                                          ; 
;@ while(P,0)                                               ; search for colored ball
;@  concat(V1,"!#",G,":",@N)                                ; identify Nth cel frame "!#G:n"
;@  add(@N,@N,1) mod(@N,@N,9)                               ; @N is next ball (0-8)
;@  letpal(P,V1)                                            ; get Nth ball palette
;@ endwhile(P)                                              ; 
;@ concat(V2,"!#",G,":",@N)                                 ; identify N+1 cel frame "!#G:n"
;@ setpal(V2,P) setpal(V1,0) let(P,0)                       ; change next ball palette
;@ timer(Rotate,500,@N)                                     ; repeat in 500 ms
